plugins {
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'com.gradle.plugin-publish' version '0.10.1'
	id "nebula.integtest" version "7.0.7"
	id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

group 'io.spring.gradle'
description = 'Sets up Javadoc and Aggregate Javadoc'

sourceCompatibility = 1.8

repositories {
	maven { url "https://repository.apache.org/content/repositories/orgapachetomcat-1257/" }
	mavenCentral()
	maven { url "https://plugins.gradle.org/m2/" }
}

dependencyLocking {
	lockAllConfigurations()
}

dependencies {
	implementation gradleApi()

	// FIXME: wait until tomcat 9.0.33 is released https://bz.apache.org/bugzilla/show_bug.cgi?id=64153
	implementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.33'
	implementation 'org.apache.tomcat.embed:tomcat-embed-el:9.0.33'
	implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:9.0.33'
	testImplementation 'org.assertj:assertj-core:3.+'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
	testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.+'
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
}

configure([compileKotlin, compileTestKotlin]) {
	kotlinOptions {
		jvmTarget = "1.8"
	}
}

pluginBundle {
	website = 'https://github.com/spring-gradle-plugins/tomcat-plugin'
	vcsUrl = 'https://github.com/spring-gradle-plugins/tomcat-plugin.git'
	description = project.description
	tags = ['Tomcat']
}

gradlePlugin {
	plugins {
		tomcatPlugin {
			id = 'io.spring.tomcat'
			displayName = 'Tomcat Plugin'
			description = 'Tomcat Plugin'
			implementationClass = 'io.spring.gradle.tomcat.TomcatPlugin'
		}
	}
}
